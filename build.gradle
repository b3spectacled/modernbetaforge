// Based on 'modern-forge-1.12-template' by by github@quat1024

buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        maven { url = 'https://repo.spongepowered.org/maven' }
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1+', changing: true
        classpath 'org.spongepowered:mixingradle:0.7-SNAPSHOT'
    }
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.spongepowered.mixin'
apply plugin: 'eclipse'
apply plugin: 'java'

version = projectMCVersion + '-' + projectVersion
group = projectGroup
archivesBaseName = projectName

java.toolchain.languageVersion = JavaLanguageVersion.of(8)
compileJava {
    sourceCompatibility = '8'
    targetCompatibility = '8'
}

repositories {
    maven { url = 'https://repo.spongepowered.org/maven' }
    maven { url = 'https://www.cursemaven.com' }
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.12.2-14.23.5.2860'
    
    implementation 'com.google.guava:guava:21.0'
    implementation 'org.spongepowered:mixin:0.8.5-SNAPSHOT'
    implementation fg.deobf('curse.maven:biomes-o-plenty-220318:2842510')
    
    annotationProcessor 'org.spongepowered:mixin:0.8.5-SNAPSHOT'
    annotationProcessor 'com.google.code.gson:gson:2.8.9'
    annotationProcessor 'com.google.guava:guava:21.0'
    annotationProcessor 'org.ow2.asm:asm-debug-all:5.2'
}

minecraft {
    mappings channel: 'stable', version: '39-1.12'
    
    runs {
        client {
            workingDirectory file("./run")
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
            mods { "${project.name}" { source sourceSets.main } }
            arg '-torg.spongepowered.asm.launch.MixinTweaker'
        }

        server {
            workingDirectory file("./run/server")
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
            mods { "${project.name}" { source sourceSets.main } }
            arg '-torg.spongepowered.asm.launch.MixinTweaker'
        }
    }
    
    accessTransformers {
        file('src/main/resources/META-INF/' + projectATFile)
    }
}

processResources {
    inputs.property 'version', project.version
  
    filesMatching('mcmod.info') {
      expand 'version': project.version
    }
}

mixin {
    add sourceSets.main, projectMixinRefmap
    config projectMixinConfig
}

jar {
    manifest.attributes(
        'FMLAT': projectATFile,
        'ForceLoadAsMod': true,
        'FMLCorePluginContainsFMLMod': 'true',
        'TweakClass': 'org.spongepowered.asm.launch.MixinTweaker',
        'TweakOrder': '0'
    )
}

sourceSets.all { it.output.resourcesDir = it.output.classesDirs.getFiles().iterator().next() }